print '#####################################################'
--Nombre del servidor
print @@servername
print 'Fecha de ejecución; ' + ((CONVERT( VARCHAR(24), GETDATE(), 121)))+ char(10)
print '#####################################################'
print '----------------------USUARIOS------------------------'+ CHAR(10)
exec sp_helplogins
go

print '------------------------------------------------------'
--SQL Logins:
SELECT
       'CREATE LOGIN ' + QUOTENAME(name) +
       ' WITH PASSWORD='+ sys.fn_varbintohexstr(password_hash) + ' HASHED, SID='       + sys.fn_varbintohexstr(sid) + ', '
       + 'DEFAULT_DATABASE='+ QUOTENAME(COALESCE(default_database_name, 'master'))
       + ', DEFAULT_LANGUAGE=' + QUOTENAME(COALESCE(default_language_name,'us_english'))
       + ', CHECK_EXPIRATION=' + CASE is_expiration_checked WHEN 1 THEN 'ON' ELSE 'OFF' END
       + ', CHECK_POLICY=' + CASE is_policy_checked WHEN 1 THEN 'ON' ELSE 'OFF' END
FROM
       sys.sql_logins
WHERE
       name<>'sa'
       AND name NOT LIKE '##MS_Policy%'
UNION ALL
--Windows logins:
SELECT
       'CREATE LOGIN ' + QUOTENAME(name) + ' FROM WINDOWS WITH '
       + 'DEFAULT_DATABASE='+ QUOTENAME(COALESCE(default_database_name, 'master'))
       + ', DEFAULT_LANGUAGE=' + QUOTENAME(COALESCE(default_language_name, 'us_english'))
FROM
       sys.server_principals
WHERE
       type IN ('U','G')
       AND name NOT LIKE '%\SQLServer2005MSSQLUser$%$%'
       AND name NOT LIKE '%\SQLServer2005SQLAgentUser$%$%'
       AND name NOT LIKE '%\SQLServer2005MSFTEUser$%$%'
       AND name NOT IN ('BUILTIN\Administrators', 'NT AUTHORITY\SYSTEM')
       AND name NOT LIKE '%NT SERVICE\%';
--Problem: when executing CREATE LOGINs generated by the query above,
--it is not possible to assign default database when that database is
--mirrored or unavailable for some other reason.
--(filled a bug for that, this needs to be solved)
--Server roles for logins:
SELECT 'EXEC sp_addsrvrolemember ' + QUOTENAME(L.name) + ', ' +
QUOTENAME(R.name)
FROM sys.server_principals L JOIN sys.server_role_members RM
ON L.principal_id=RM.member_principal_id
JOIN sys.server_principals R
ON RM.role_principal_id=R.principal_id
WHERE L.type IN ('U','G','S')
AND L.name NOT LIKE '%\SQLServer2005MSSQLUser$%$%'
AND L.name NOT LIKE '%\SQLServer2005SQLAgentUser$%$%'
AND L.name NOT LIKE '%\SQLServer2005MSFTEUser$%$%'
AND L.name NOT IN ('BUILTIN\Administrators', 'NT AUTHORITY\SYSTEM', 'sa')
AND L.name NOT LIKE '%NT SERVICE\%';
